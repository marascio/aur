# Maintainer: Louis R. Marascio <lrm@fitnr.com>
# Contributor: Cody Maloney <cmaloney@theoreticalchaos.com>

pkgname=gtest
pkgver=1.5.0
pkgrel=3
pkgdesc="Google Test - C++ testing utility based on the xUnit framework (like JUnit)"
arch=('i686' 'x86_64')
url="http://code.google.com/p/googletest/"
license=('BSD')
options=('!libtool')
depends=('gcc-libs' 'sh')
makedepends=('make' 'gcc' 'python2')
source=(http://googletest.googlecode.com/files/$pkgname-$pkgver.tar.gz)
md5sums=('7e27f5f3b79dd1ce9092e159cdbd0635')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i "0,/\/usr\/bin\/env python/s/python/python2/" scripts/*.py
  ./configure --prefix=/usr || return 1
  make || return 1
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install || return 1
  install -Dm0644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
